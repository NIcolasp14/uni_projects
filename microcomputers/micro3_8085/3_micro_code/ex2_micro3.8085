MVI D,55H          ; Κατώφλι 1, Κ1 = 255/3 = 85 = 55Η
MVI E,AAH          ; Κατώφλι 2, Κ2 = 2(255/3) = 170 = AΑH 
 
MVI A,10H          ; Σβήσιμο digits, εκτός των δύο πρώτων
STA 0B02H	
STA 0B03H	
STA 0B04H	
STA 0B05H	

MVI A,0DH          ; Μάσκα για RST 6.5
SIM                ; Αποθήκευση μάσκας διακοπής στον accumulator A
EI	            ; Ενεργοποίηση διακοπής τύπου 6.5 RST

WAIT:
	JMP WAIT    ; Ατέρμονη επανάληψη μέχρι να έρθει διακοπή τύπου RST 6.5

INTR_ROUTINE:      ; Ρουτίνα διακοπής
	CALL KIND   ; Είσοδος πληκτρολογίου (μονάδες) και αποθήκευσή της στον acc A
	STA 0B00H   ; Αποθήκευση εισόδου στο πρώτο digit
	MOV B,A     ; Αποθήκευση μονάδων στην βοηθητική μεταβλητή Β ως μονάδες hex αριθμού
	CALL KIND   ; Είσοδος πληκτρολογίου ξανά (δεκαεξάδες) και αποθήκευσή της στον acc A
	STA 0B01H   ; Αποθήκευση εισόδου στο δεύτερο digit
	RLC         ; 4 rotations για πολλαπλασιαμό με 16 και εύρεση δεκαεξάδων 
	RLC
	RLC 
	RLC		
	ADD B       ; Πρόσθεση μονάδων και δεκαεξάδων για τελική διαμόρφωση του 
		     ; hex αριθμού εισόδου
	PUSH PSW    ; Αποθήκευση Α στην στοίβα, καθώς περιέχει χρήσιμα για μετά δεδομένα
	PUSH D      ; Το ίδιο και για τον διπλό καταχωρητή DE (περιέχουν τα κατώφλια)
	LXI D,0B00H ; Δίνουμε στον D την διεύθυνση του πρώτου digit
	CALL STDM   ; Μετακινεί το μήνυμα στην θέση που περιμένει να το βρει η ρουτίνα DCD
	CALL DCD    ; Ενημερώνει το display
	POP D       ; Δίνει στο D την προηγούμενη τιμή του (κατώφλι 2), από την στοίβα
	POP PSW     ; To ίδο και στον Α που περιείχε την τιμή της εισόδου σε hex μορφή

	MOV B,D     ; Χρησιμοποιούμε βοηθητικά τον Β γιατί αναφέρεται ρητά ότι τα D,E 
		     ; περιέχουν τις τιμές των κατωφλιών
	INR B       ; Αύξηση του Β για την απαραίτητη σύγκριση
	CMP B       ; Σύγκριση αν Α =< 55Η 
	JC LT_56    ; Αν ναι πήγαινε στην ετικέτα για άναμμα του πρώτου LED
	MOV B,E     ; Αν όχι, ακολουθούμε την ίδια διαδικασία για το κατώφλι 2
	INR B
	CMP B         ; Σύγκριση αν Α =< ΑΒΗ
	JC LT_AB      ; Αν ναι, πήγαινε στην ετικέτα για άναμμα του δεύτερου LED
	MVI A,04H     ; Αν όχι, ετοίμασε το τρίτο LED
	JMP LED_IT_GO ; και πήγαινε να το ανάψεις

LT_56: 
	MVI A,01H     ; Πρώτο LED
	JMP LED_IT_GO

LT_AB:
	MVI A,02H     ; Δεύτερο LED. Δεν χρειάζεται JMP καθώς το άναμμά του γίνεται
		       ; ακριβώς παρακάτω

LED_IT_GO: 
	CMA           ; Συμπλήρωμα ως προς 1 του Α, ώστε μέσω αντίστροφης λογικής,
	              ; να ανάψει το σωστό LED
	STA 3000H     ; ’ναμμα LED
	EI            ; Ενεργοποίηση για πιθανή επόμενη διακοπή τύπου 6.5 RST 
	JMP WAIT      ; Επιστροφή στο να περιμένουμε διακοπή

END
