MVI A,0DH            ; Μάσκα της διακοπής RST 6.5
SIM                  ; Αποθήκευση μάσκας διακοπής στον accumulator A
EI	              ; Ενεργοποίηση διακοπής τύπου RST 6.5

WAIT:			
	JMP WAIT      ; Ατέρμονη επανάληψη μέχρι να έρθει διακοπή τύπου RST 6.5

INTR_ROUTINE:		
	EI	       ; Η ενεργοποίηση αυτή χρησιμοποιείται για πιθανές επόμενες διακοπές
	MVI A,10H     ; Σβήνω τα υπόλοιπα digits (θέσεων 1,2,5,6) της 7-segment display,  
	STA 0B00H     ; μεταφέροντας την τιμή 10Η στις αντίστοιχες θέσεις μνήμης 
	STA 0B01H		
	STA 0B04H
	STA 0B05H
	LXI B,03E8H   ; BC <- 1000 (δεκαδικό), ώστε να έχουμε καθυστέρηση 1000ms = 1s

INITIALISE_DEKADES:		
	MVI A,06H     ; Αρχικοποιώ τις δεκάδες με 6, αλλά στην ετικέτα DECADES τις μειώνω
	STA 0B03H     ; για να έχω 50s. Μεταφορά στην θέση μνήμης του τέταρτου digit

INITIALISE_MONADES: 	
	MVI A,0AH     ; Αρχικοποιώ τις μονάδες με 10. Παρακάτω, στην ετικέτα MONADES
	STA 0B02H     ; γίνονται ίσες με 9, πριν τις εμφανίσουμε. Έχω 59s, δηλαδή, αρχικά.
		       ; Η θέση μνήμης των μονάδων αντιστοιχεί στο τρίτο digit
DEKADES: 		
	LDA 0B03H     ; Φέρνω από την μνήμη τις δεκάδες και τις βάζω στον Α
	DCR A         ; Μειώνω τις δεκάδες όταν αρχικοποιήσω τις μονάδες
	STA 0B03H     ; Τις αποθηκεύω στην θέση των δεκάδων (τέταρτο digit)
		  
MONADES:		
	LDA 0B02H     ; Φέρνω από την μνήμη τις μονάδες και τις βάζω στον Α
	DCR A         ; Μειώνω τις μονάδες
	STA 0B02H     ; Τις αποθηκεύω στην θέση των μονάδων (τρίτο digit)
			
	LXI D,0B00H   ; Φορτώνω στον D την θέση μνήμης του πρώτου digit, ώστε να τυπωθούν
	CALL STDM     ; το τρίτο και το τέταρτο στις μεσαίες θέσεις, όπου και θέλουμε
	CALL DCD      ; Εμφάνιση στην οθόνη
	CALL DELB     ; Καθυστέρηση 1s για κάθε μονάδα
						
LEDS_ON:		; Άναμμα των LEDs
	MVI A,00H   
	STA 3000H

DIGIT_DISPLAY:  
	LDA 0B02H               ; Μονάδες
	CPI 00H                 ; Σύγκριση με το 0 
	JNZ MONADES             ; Αν δεν είναι 0, συνεχίζει το countdown των μονάδων
	LDA 0B03H               ; Δεκάδες
	CPI 00H                 ; Σύγκριση με το 0
	JNZ INITIALISE_MONADES  ; Αν δεν είναι 0, αρχικοποιώ τον μετρητή μονάδων  
	 	                 ; και συνεχίζω το countdown των δεκάδων
LEDS_OFF:		          ; Αν φτάσεις εδώ, σβήσε τα LEDs 
	MVI A,FFH  
	STA 3000H  

	JMP WAIT                ; και συνέχισε να περιμένεις μέχρι να έρθει νέα διακοπή
END	
